// FB Controller for Stage 2 (Machining Center - MC)
// - Machines raw materials into product bases
// - Manage internal state transisitions using a state machine
// - Sets local 'readyForPart' flag to signal upstream to provide raw material
// States:
// - IDLE: Enter WAITING if system is enabled
// - WAITING: Send start signal to the MC, if MC is busy reset 'ReadyForPart' flag and enter RUNNING
// - RUNNING: Enter COMPLETE when MC stops being busy
// - COMPLETE: Set 'ReadyForPart" flag, enter WAITING if system is still on, else IDLE
FUNCTION_BLOCK Stage2_MC
VAR_INPUT	
	iBusy : BOOL;					// Machining Station is currently processing
	iOpened : BOOL;					// CNC door open
	iError : BOOL;					// Non-raw material detected at station entrance
	iProgress : WORD;				// CNC progress (0 - 100)
END_VAR
VAR_OUTPUT
	oStart: BOOL;					// Start signal for MC, starts machining when part is detected at entry
	oStop: BOOL;					// MC Stop signal
	oReset: BOOL;					// MC Reset signal
	oProduceLids: BOOL;				// TRUE: MC produces lids, FALSE: MC produces bases
END_VAR
VAR
	SystemOn : BOOL;				// Local SystemEnable flag
	State : STATE := IDLE;			// Initiate to IDLE
	isReadyForPart: BOOL := TRUE; 	// Local flag signals Stage One to provide raw material
	
	MC_Controller : FB_MachineCenter;
END_VAR


// Instantiate Machining Center
MC_Controller(
	iBusy := iBusy, 
	iOpened := iOpened, 
	iError := iError, 
	iProgress := iProgress);
	oStart := MC_Controller.oStart;
	oStop := MC_Controller.oStop;
	oReset := MC_Controller.oReset;
	oProduceLids := MC_Controller.oProduceLids;
	
MC_Controller.ProduceLids := FALSE;

// Check system is enabled
SystemOn := GV_Flags.SystemEnable;

CASE State OF
	IDLE :
		MC_Controller.Enabled := FALSE;
		IF SystemOn THEN
			State := WAITING;
		END_IF
	WAITING:
		MC_Controller.Enabled := TRUE;
		IF MC_Controller.IsBusy THEN
			isReadyForPart := FALSE;
			State := RUNNING;
		END_IF
	RUNNING:
		IF NOT MC_Controller.IsBusy THEN
			State := COMPLETE;
		END_IF
	COMPLETE:
		isReadyForPart := TRUE;
		IF SystemOn THEN
			State := WAITING;
		ELSE
			State := IDLE;
		END_IF
END_CASE


PROPERTY ReadyForPart : BOOL
GET
ReadyForPart := isReadyForPart;