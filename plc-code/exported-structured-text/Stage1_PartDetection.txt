// FB Controller for the First Stage of the production process (item detection, rejection, and conveyor control)
// - Monitor sensor inputs (Vision sensor, actuator limit switches)
// - Control outputs  (conveyor motor, pusher actuator)
// - Reject metal raw materials by pushing them off the conveyor, while accepting blue materials
// - Manage internal state transisitions using a state machine
// States:
// - IDLE: Enter RUNNING state if start trigger is TRUE and Stage 2 is ready for material
// - RUNNING: Conveyor on, reject metal raw materials, Enter IDLE if Stage 2 not ready for material
FUNCTION_BLOCK Stage1_PartDetection
VAR_INPUT
	iStage2Ready : BOOL;			// Ready signal from Stage 2
	iVisionSensorBit0 : BOOL;		// LSB, Vision Sensor output
	iVisionSensorBit1 : BOOL;
	iVisionSensorBit2 : BOOL;
	iVisionSensorBit3 : BOOL;		// MSB
	iPusherBackLimit : BOOL;		// Back (reset) position actuator limit switch (NO)
	iPusherFrontLimit : BOOL;		// Front (extended) position actuator limit switch (NO)
	iPusherEntrySensor : BOOL;		// True when object is detected at the entrance of the pusher
END_VAR
VAR_OUTPUT
	oConveyor : BOOL;				// Turn on conveyor motor when TRUE
	oPusher : BOOL;					// Extend pusher arm when TRUE
END_VAR

VAR
	SystemOn: BOOL;					// Local system enabled flag
	ConveyorOn : BOOL;				// Conveyor on signal
	E_State : E_States := E_States.Idle;			// Set intial state
	Reject : BOOL;					// Used to reject unwanted parts

	// Controllers
	Pusher : FB_PusherController;
	Conveyor : FB_ConveyorController;
	VisionSensor : FB_VisionSensorAD;
END_VAR


// Check if system is enabled
SystemOn := GV_Flags.SystemEnabled;

// Instantiate Controller FBs
Conveyor(iRun := ConveyorOn);
oConveyor := Conveyor.oRun;
Pusher(iFrontLimit := iPusherFrontLimit, iBackLimit := iPusherBackLimit);
oPusher := Pusher.oPush;
VisionSensor(iBit0 := iVisionSensorBit0, iBit1 := iVisionSensorBit1, iBit2 := iVisionSensorBit2, iBit3 := iVisionSensorBit3);

// Safety stop
IF NOT SystemOn THEN
	E_State := E_States.Idle;
END_IF

CASE E_State OF
	E_States.Idle : 
		ConveyorOn := FALSE;
		IF SystemOn AND iStage2Ready THEN
			E_State := E_States.Running;
		END_IF
	E_States.Running :  
		ConveyorOn := TRUE;
		// Process Vision Sensor Output
		THIS^.ProcessObjectDetection();
		
		IF NOT SystemOn OR NOT iStage2Ready THEN
			E_State := E_States.Idle;
		END_IF
END_CASE


// Processes sensor output
// When an object is detected, determines whether it should be accepted or rejected
// If part is to be accepted, sets the accepted bit, otherwise pushes the part off the conveyor
METHOD ProcessObjectDetection : BOOL
VAR
	Object: STRING;
END_VAR

// If object should be rejected, wait for it to be at the entrance of pusher before executing
IF Reject AND iPusherEntrySensor THEN
	Pusher.Execute();
	Reject := FALSE;	// Reset Reject flag
END_IF

IF VisionSensor.bObjectDetected THEN
	Object := VisionSensor.sObjectDetected;
	// Check if object should be rejected
	IF Object = 'metal raw material' OR Object = 'unknown' THEN
		Reject := TRUE;
	END_IF
END_IF
